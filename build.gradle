plugins {
  id "java"

  id "maven-publish"

  id "edu.wpi.first.GradleRIO" version "2022.4.1"
  id "com.diffplug.spotless" version "6.1.0"
}

group = 'io.github.tigerbotics7125'
version = '1.0.0'

sourceCompatibility = JavaVersion.VERSION_11
targetCompatibility = JavaVersion.VERSION_11

dependencies {
  implementation wpi.java.deps.wpilib()
  implementation wpi.java.vendor.java()

  testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.1'
  testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.1'
}

publishing {
  publications {
    maven(MavenPublication) {
      groupId = "${groupId}.tigerlib"
      artifactId = "library"
      version = "${version}"

      from components.java
    }
  }
}

spotless {
  java {
    target fileTree('.') {
      include '**/*.java'
      exclude '**/build/**', '**/build-*/**'
    }
    toggleOffOn()
    googleJavaFormat('1.8').aosp().reflowLongStrings()
    licenseHeader '/**\n * Copyright (C) $YEAR, Tigerbotics\' team members and all other contributors.\n * Open source software; you can modify and/or share this software.\n */'
    removeUnusedImports()
    trimTrailingWhitespace()
    endWithNewline()
  }
  groovyGradle {
    target fileTree('.') {
      include '**/*.gradle'
      exclude '**/build/**', '**/build-*/**'
    }
    greclipse()
    indentWithSpaces(2)
    trimTrailingWhitespace()
    endWithNewline()
  }
  format 'misc', {
    target fileTree('.') {
      include '**/*.md', '**/.gitignore', '**/*.yaml', '**/*.yml'
      exclude '**/build/**', '**/build-*/**'
    }
    trimTrailingWhitespace()
    indentWithSpaces(2)
    endWithNewline()
  }
}
